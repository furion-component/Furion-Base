plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.huibur.furion'
version = '1.1.1'
//编译属性
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    springWeb = '5.2.4.RELEASE'
    springContext = '5.2.4.RELEASE'
    slf4jApi = '1.7.30'
    commonsLang3 = '3.9'
    servletApi = '4.0.1'
}

dependencies {
    implementation "org.springframework:spring-web:$springWeb"
    implementation "org.springframework:spring-context:$springContext"
    implementation "org.slf4j:slf4j-api:$slf4jApi"
    implementation "org.apache.commons:commons-lang3:$commonsLang3"
    compileOnly "javax.servlet:javax.servlet-api:$servletApi"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'furion-base'
            version '1.1.0'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'furion base'
                description = 'a base module for java web'
                url = 'https://github.com/furion-component/furion-base.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                organization {
                    name = 'HuiBur'
                    url = 'https://www.huibur.com'
                }
                developers {
                    developer {
                        id = 'wenzhijie'
                        name = 'wenzhijie'
                        email = 'wenzhi_jie@126.com'
                        organization = 'HuiBur'
                        organizationUrl = 'https://www.huibur.com'
                        roles = ['owner', 'developer']
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/furion-component/furion-base'
                    developerConnection = 'scm:git:ssh://git@github.com:furion-component/furion-base.git'
                    url = 'https://github.com/furion-component/furion-base.git'
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
    }
//    if (JavaVersion.current().isJava9Compatible()) {
//        options.addBooleanOption('html5', true)
//    }
}